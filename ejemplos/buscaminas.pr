que exploto valga 0
que largo valga 0
que numero_magico valga 0

que minas valga []
que fila_minas valga []
que campo valga []
que fila_campo valga []

hasta que "largo es mayor que 2 y ademas largo es menor que o es 16" hacer:
  mostrar 'Ingresá el tamaño del tablero (de 3 a 16)'
  leer numero a largo
y repetir.

que indice valga 0
mientras que "indice es menor que largo" hacer:
  que fila_minas valga fila_minas + 0
  que fila_campo valga fila_campo + '-'
  que minas valga minas + '.'
  que campo valga campo + '.'
  incrementar indice
y repetir.

que indice valga 0
mientras que "indice es menor que largo" hacer:
  que minas[indice] valga fila_minas
  que campo[indice] valga fila_campo
  incrementar indice
y repetir.

hasta que "numero_magico es mayor que 999 y ademas numero_magico es menor que 10000" hacer:
  mostrar 'Ingresá un número de 4 dígitos'
  leer numero a numero_magico
y repetir.

mostrar '** BUSCAMINAS **'

que fila_a_mostrar valga 0
mientras que "fila_a_mostrar es menor que largo" hacer:
  mostrar campo[fila_a_mostrar]
  incrementar fila_a_mostrar
y repetir.

que fil valga 0
mientras que "fil es menor que largo" hacer:
  que col valga 0
  mientras que "col es menor que largo" hacer:
    que numero_magico valga (numero_magico * 2377 + 16493)
    si pasa que "numero_magico es menor que 0" entonces:
      que numero_magico valga numero_magico * -1
    listo.

    que rand valga numero_magico - 19717 * (numero_magico/19717)
    que rand valga rand - 4 * (rand / 4)

    si pasa que "rand es 1" entonces:
      que minas[fil][col] valga '*'
    listo.

    incrementar col
  y repetir.
  incrementar fil
y repetir.

mientras que "exploto es 0" hacer:
  que fila valga -1
  que columna valga -1

  hasta que "fila es mayor que o es 0 y ademas fila es menor que largo" hacer:
    mostrar 'Ingresá una Fila entre 0 y ' + (largo - 1)
    leer numero a fila
  y repetir.

  hasta que "columna es mayor que o es 0 y ademas columna es menor que largo" hacer:
    mostrar 'Ingresá una Columna entre 0 y ' + (largo - 1)
    leer numero a columna
  y repetir.

  si pasa que "minas[fila][columna] es '*'" entonces:
    mostrar 'Perdiste! :('
    que campo[fila][columna] valga '*'
    que exploto valga 1
  si no:
    que bombas_alrededor valga 0
    que i valga fila - 1
    mientras que "i es menor que o es (fila + 1)" entonces:
      que j valga columna - 1
      mientras que "j es menor que o es (columna + 1)" entonces:
        si pasa que "i es mayor que o es 0 y ademas j es mayor que o es 0 y ademas i es menor que largo
          y ademas j es menor que largo y ademas minas[i][j] es '*'" entonces:
          incrementar bombas_alrededor
        listo.
        incrementar j
      y repetir.
      incrementar i
    y repetir.

    que campo[fila][columna] valga bombas_alrededor
  listo.

  que fila_a_mostrar valga 0
  mientras que "fila_a_mostrar es menor que largo" hacer:
    mostrar campo[fila_a_mostrar]
    incrementar fila_a_mostrar
  y repetir.

  que gano valga 1
  que fila valga 0
  mientras que "gano es 1 y ademas fila es menor que largo" hacer:
    que columna valga 0
    mientras que "columna es menor que largo" hacer:
      si pasa que "minas[fila][columna] no es '*'
        y ademas campo[fila][columna] es '-' " entonces:
        que gano valga 0
      listo.
      incrementar columna
    y repetir.
    incrementar fila
  y repetir.

  si pasa que "gano es 1" entonces:
    mostrar 'Ganaste! :)'
    terminar.
  listo.
y repetir.
